#version 450 core
#include "declarations.glsl"
#include "hash.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

layout(binding = 0) uniform UniformBlock {
	int slot;
} ubo;

layout(binding = 1) uniform utexture2DArray heightmaps;
layout(rgba8, binding = 2) writeonly uniform image2DArray bent_normals;

layout(set = 0, binding = 3, std430) /*readonly*/ buffer NodeSlots {
	Node nodes[];
};

const uint HEIGHTMAP_BORDER = 4;

shared float heights[16][16];

void main() {
    ivec3 base_pos = ivec3(ivec2(gl_GlobalInvocationID.xy + HEIGHTMAP_BORDER - 4), ubo.slot);
    heights[gl_LocalInvocationID.x][gl_LocalInvocationID.y] = extract_height_above_water(texelFetch(heightmaps, base_pos, 0).x);
    heights[gl_LocalInvocationID.x+8][gl_LocalInvocationID.y] = extract_height_above_water(texelFetch(heightmaps, base_pos+ivec3(8,0,0), 0).x);
    heights[gl_LocalInvocationID.x][gl_LocalInvocationID.y+8] = extract_height_above_water(texelFetch(heightmaps, base_pos+ivec3(0,8,0), 0).x);
    heights[gl_LocalInvocationID.x+8][gl_LocalInvocationID.y+8] = extract_height_above_water(texelFetch(heightmaps, base_pos+ivec3(8,8,0), 0).x);
    barrier();

	Node node = nodes[ubo.slot];
	float spacing = 19545.9832 / float(1 << node.level);

    vec4 value = vec4(1);
    float height = heights[gl_LocalInvocationID.x+4][gl_LocalInvocationID.y+4];

    for (int y = -2; y <= 2; y++) {
        for (int x = -2; x <= 2; x++) {
            if (x != 0 || y != 0) {
                float h = heights[gl_LocalInvocationID.x+x+4][gl_LocalInvocationID.y+y+4] - height;
                if (h > 0)
                    value.a -= 1.0 / 24 * smoothstep(spacing*.25, spacing*.5, h);
            }
        }
    }

    imageStore(bent_normals, ivec3(gl_GlobalInvocationID.xy, node.layer_slots[BENT_NORMALS_LAYER]), value);
}
