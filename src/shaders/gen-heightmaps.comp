#version 450 core
#include "declarations.glsl"
#include "hash.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

layout(binding = 0) uniform UniformBlock {
	ivec2 position;
	ivec2 origin;
	float spacing;
	int in_slot;
	int out_slot;
	int level_resolution;
	uint _face;
} ubo;

layout(binding = 1) uniform utexture2D heightmaps_in;
layout(r32ui, binding = 2) writeonly uniform uimage2D heightmaps_out;

layout(set = 0, binding = 3, std430) /*readonly*/ buffer NodeSlots {
	Node nodes[];
};

shared vec4 h[10][10];

void main() {
	Node node = nodes[ubo.out_slot];

	uint x = gl_LocalInvocationID.x;
	uint y = gl_LocalInvocationID.y;

	// Load parent heights
	if (x < 5 && y < 5) {
		ivec2 origin = ivec2(ubo.origin+gl_WorkGroupID.xy*4);
		ivec2 gpos = origin.xy + ivec2(gl_LocalInvocationID.xy*2-ivec2(2));
		h[x*2][y*2].x = extract_height(texelFetch(heightmaps_in, gpos, 0).x);
		h[x*2+1][y*2].x = extract_height(texelFetch(heightmaps_in, gpos + ivec2(1,0), 0).x);
		h[x*2][y*2+1].x = extract_height(texelFetch(heightmaps_in, gpos + ivec2(0,1), 0).x);
		h[x*2+1][y*2+1].x = extract_height(texelFetch(heightmaps_in, gpos + ivec2(1,1), 0).x);
	}
	barrier();

	// Compute slopes
	if (x < 8 && y < 8) {
		h[x+1][y+1].y = (h[x+2][y+1].x - h[x][y+1].x) * 0.5;
		h[x+1][y+1].z = (h[x+1][y+2].x - h[x+1][y].x) * 0.5;
		h[x+1][y+1].w = (h[x+2][y+2].x - h[x][y+1].x - h[x+1][y].x + h[x+1][y+1].x) * 0.5;
	}
	barrier();

	// Bicubic Interpolation
	x = x/2 + 2;
	y = y/2 + 2;
	vec2 t = vec2(gl_GlobalInvocationID.xy%2)/2.0;


	mat4 Binv = mat4(1, 0, -3, 2,
					 0, 0, 3, -2,
					 0, 1, -2, 1,
					 0, 0, -1, 1);
	mat4 F = mat4(h[x][y].x,   h[x+1][y].x,   h[x][y].y,   h[x+1][y].y,
				  h[x][y+1].x, h[x+1][y+1].x, h[x][y+1].y, h[x+1][y+1].y,
				  h[x][y].z,   h[x+1][y].z,   h[x][y].w,   h[x+1][y].w,
				  h[x][y+1].z, h[x+1][y+1].z, h[x][y+1].w, h[x+1][y+1].w);
	mat4 M = Binv * F * transpose(Binv);
	vec4 xx = vec4(1, t.x, t.x*t.x, t.x*t.x*t.x);
	vec4 yy = vec4(1, t.y, t.y*t.y, t.y*t.y*t.y);
	vec4 ddx = vec4(0, 1, 2*t.x, 3*t.x*t.x);
	vec4 ddy = vec4(0, 1, 2*t.y, 3*t.y*t.y);
	float height = dot(xx * M, yy);
	float dx = dot(ddx * M, yy) / ubo.spacing;
	float dy = dot(xx * M, ddy) / ubo.spacing;
	// float dxy = dot(ddx * M, ddy) / (ubo.spacing*ubo.spacing);
	float slope = length(vec2(dx, dy));


	// Calculate position
	ivec3 position;
	ivec2 position2 = ivec2(gl_GlobalInvocationID.xy)+ubo.position;
	if (node.face == 0) position = ivec3( ubo.level_resolution/2,   position2.x,             -position2.y);
	if (node.face == 1) position = ivec3(-ubo.level_resolution/2,  -position2.x,             -position2.y);
	if (node.face == 2) position = ivec3( position2.x,              ubo.level_resolution/2,   position2.y);
	if (node.face == 3) position = ivec3(-position2.x,             -ubo.level_resolution/2,   position2.y);
	if (node.face == 4) position = ivec3( position2.x,             -position2.y,              ubo.level_resolution/2);
	if (node.face == 5) position = ivec3(-position2.x,             -position2.y,             -ubo.level_resolution/2);

	// If we are on the edge of a tile, then make sure our slope matches what is on the other side
	// of the seam (by setting both to zero).
	if (any(greaterThanEqual(abs(position2), ivec2(ubo.level_resolution/2)))) {
		slope = 0;
	}

	// Apply noise
	float n = pow(random(uvec3(position+ubo.level_resolution/2)), 2);
	height += n * ubo.spacing * mix(0.1, 0.4, smoothstep(0.4, 0.5, slope));

	uint encoded_height = uint((height + 1024) * 512.0) | (height < 0 ? 0x800000 : 0);

	// Write output
	imageStore(heightmaps_out, ivec2(gl_GlobalInvocationID.xy), uvec4(encoded_height, 0, 0, 0));
}
