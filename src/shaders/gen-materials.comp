#version 450 core
#include "declarations.glsl"
#include "hash.glsl"

layout(local_size_x = 4, local_size_y = 4) in;

layout(binding = 0) uniform UniformBlock {
	ivec2 heightmaps_origin;
	ivec2 parent_origin;
	int heightmaps_slot;
	int normals_slot;
	int albedo_slot;
	int parent_slot;
	float spacing;
	int padding;
} ubo;

layout(r32ui, binding = 1) readonly uniform uimage2DArray heightmaps;
layout(rgba32ui, binding = 2) writeonly uniform uimage2D bc5_staging;

layout(binding = 3) uniform texture2D albedo_in;
layout(rgba8, binding = 4) writeonly uniform image2D albedo_out;

layout(set = 0, binding = 5) uniform texture2D noise;
layout(set = 0, binding = 6) uniform sampler linear_wrap;

shared vec2 group_normals[16];

void main() {
	ivec3 in_pos = ivec3(gl_GlobalInvocationID.xy + ubo.heightmaps_origin, ubo.heightmaps_slot);
	ivec2 out_pos = ivec2(gl_GlobalInvocationID.xy);

	uint e00 = imageLoad(heightmaps, in_pos).x;
	uint e10 = imageLoad(heightmaps, in_pos + ivec3(1,0,0)).x;
	uint e01 = imageLoad(heightmaps, in_pos + ivec3(0,1,0)).x;
	uint e11 = imageLoad(heightmaps, in_pos + ivec3(1,1,0)).x;

	bool is_water = dot((uvec4(e00, e10, e01, e11) & uvec4(0x800000)) >> 23, uvec4(1)) > 2;

	float h00 = extract_height(e00);
	float h10 = extract_height(e10);
	float h01 = extract_height(e01);
	float h11 = extract_height(e11);

	vec3 normal = vec3(0,1,0);
	if (!is_water) {
		normal = vec3(h10 + h11 - h00 - h01,
						2.0 * ubo.spacing,
						-1.0 * (h01 + h11 - h00 - h10));
		normal = normalize(normal);
	}

	vec4 noise_value = vec4(0.5);//texture(sampler2D(noise, linear_wrap), vec2(world_pos.xy*.0001));

	vec4 albedo_roughness = vec4(.011, .03, .003, 0.7);
	float rock = 1-smoothstep(0.80, .95, normal.y);

	float grass_fraction = mix(0, .3, smoothstep(0.95, 1, normal.y));
	float grass = step(grass_fraction, dot(noise_value, vec4(.25)));
	albedo_roughness.rgb = mix(vec3(.03,.02,0), vec3(0,.1,0), grass);

	albedo_roughness.rgb = mix(albedo_roughness.rgb, vec3(0.02), rock);

	if (ubo.parent_slot >= 0) {
		vec2 nv = guassian_random(gl_GlobalInvocationID.xy);
		ivec2 offset = clamp(ivec2(round(nv)), ivec2(-1), ivec2(1));
		vec4 p = texelFetch(albedo_in, ivec2(ubo.parent_origin + (out_pos+offset)/2), 0);

		// HACK: We want to avoid blending in water texels onto the land. Since those texels are 
		// known to have low roughness, we can filter them with this check. If the lookup fails,
		// we use albedo and roughness values for sand.
		if (p.a > 0.5) {
			albedo_roughness = p;
		} else {
			albedo_roughness = vec4(.2, .2, .15, .8);
		}
	}

	if (is_water) {
		albedo_roughness.a = 0.1;
		float negative_depth = min(h00 + h10 + h01 + h11, 0);
		albedo_roughness.rgb = mix(vec3(0,.03,.2), albedo_roughness.rgb, exp(negative_depth * vec3(5,.5,.5)));
	}

	// if (ubo.normals_slot >= 0)
	// 	imageStore(normals, ivec3(out_pos, ubo.normals_slot), vec4(normal.x*0.5+0.5, normal.z*0.5+0.5,0.0, 0.0));
	if (ubo.albedo_slot >= 0)
		imageStore(albedo_out, out_pos, albedo_roughness);

	if (gl_LocalInvocationID == uvec3(0)) {
		for (int i = 0; i < 16; i++)
			group_normals[i] = vec2(0);
	}

	barrier();

	group_normals[gl_LocalInvocationID.x + 4*gl_LocalInvocationID.y] =
		vec2(normal.x*0.5+0.5, normal.z*0.5+0.5);

	barrier();

	if (gl_LocalInvocationID == uvec3(0)) {
		vec2 nmin = group_normals[0];
		vec2 nmax = group_normals[0];
		for (int i = 1; i < 16; i++) {
			nmin = min(nmin, group_normals[i]);
			nmax = max(nmax, group_normals[i]);
		}

		uvec2 qnmin = clamp(uvec2(floor(nmin * 255.0)), uvec2(0), uvec2(254));
		uvec2 qnmax = clamp(uvec2(ceil(nmax * 255.0)), qnmin+uvec2(1), uvec2(255));

		nmin = vec2(qnmin) / 255.0;
		nmax = vec2(qnmax) / 255.0;

		uvec2 weights[16];
		uint permute[8] = uint[](1, 7, 6, 5, 4, 3, 2, 0);
		for (int i = 0; i < 16; i++) {
			weights[i] = uvec2(7.0 * (group_normals[i] - nmin) / (nmax - nmin));
			weights[i] = uvec2(permute[weights[i].x], permute[weights[i].y]);
		}

		uvec4 bc5_output = uvec4(0);
		bc5_output.xz = qnmax | qnmin << 8
			| weights[0] << 16 | weights[1] << 19 | weights[2] << 22
			| weights[3] << 25 | weights[4] << 28 | (weights[5]&1) << 31;
		bc5_output.yw = ((weights[5]&6) >> 1) | weights[6] << 2 | weights[7] << 5 | weights[8] << 8
			| weights[9] << 11 | weights[10] << 14 | weights[11] << 17 | weights[12] << 20
			| weights[13] << 23 | weights[14] << 26 | weights[15] << 29;

		imageStore(bc5_staging, ivec2(gl_WorkGroupID.xy), bc5_output);
	}
}
